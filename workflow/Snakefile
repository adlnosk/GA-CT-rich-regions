## Modules
import os
import numpy as np
import gzip

import sys
print(sys.version)

configfile: "../config/config_paths.yaml"

print (config['species'])

 
# get parent of current directory
PATH = os.getcwd()
OUT = os.path.abspath(os.path.join(PATH, os.pardir))
SPEC = config['species']
OUT_DIR = f"{OUT}/results/"

def get_raw_reads(wildcards):
    return config["raw_reads"][wildcards.spec]
    
def get_assembly(wildcards):
    return config["assembly"][wildcards.spec]

def get_fastq(wildcards):
    return config["fastq"][wildcards.spec]

def get_fail_reads(wildcards):
    return config["fail_reads"][wildcards.spec]

# Adjusting the requested outputs according to the config file
def which_analysis():
    analysis_inputs = [     
       #expand (OUT_DIR + "/list_GACT_regions_in_assembly.txt", spec=SPEC), #list of all regions
       expand (OUT_DIR + "{spec}/list_contigs_ends.txt", spec=SPEC), #list of contigs that are broken due to GACT
       expand (OUT_DIR + "{spec}/PLOTS/curves_contigs_ends.pdf", spec=SPEC), #plot contig ends
       expand (OUT_DIR + "{spec}/list_multimapped_raw_reads_ends.txt", spec=SPEC), #list reads, that are mapping to 2 contig ends (possible bridge)
    ]
    # Optionals analysis steps
    if config["plot_contig_curves_all"] == "True":
        analysis_inputs.append(
	    expand (OUT_DIR + "{spec}/PLOTS/curves_assembly_all.pdf", spec=SPEC),
        )
    if config["detect_fail_regions"] == "True": 
        analysis_inputs.append(
            expand (OUT_DIR + "{spec}/list_multimapped_fail_reads_ends.txt", spec=SPEC),
        )
    return analysis_inputs

print(which_analysis())

"""
Rules
"""

rule all:
    input:
        expand (OUT_DIR + "{spec}/list_GACT_regions_in_assembly.txt", spec=SPEC), #list of all regions
        which_analysis()

# FAIL READS into FASTQ
rule zip_fastq_fail:
    input:
        get_fail_reads # 1 bam file
    output:
        temp(OUT_DIR + "{spec}/mapped_reads/{spec}_fail.fq.gz")
    threads: 5
    resources: 
        mem_mb=50000
    shell:
        """
        module load bioinfo/samtools/1.19; module load devel/pigz/2.8; samtools sort -@ 5 -n {input} | samtools fastq --threads 5 | pigz -f -p 5 > {output}
        """

# Get GA/CT rich regions in the assembly, RAW reads and in the FAIL reads

rule GA_CT_assembly:
    input: 
        fasta_file=get_assembly
    output:
        out=OUT_DIR + "{spec}/list_GACT_regions_in_assembly.txt"
    conda:
        "envs/py.yml"
    params:
        window_size=50,
        threshold=0.95
    script:
        "scripts/GACT_assembly.py"

rule GA_CT_reads:
    input: 
        fastq_gzip_file=get_fastq
    output:
        out=temp(OUT_DIR + "{spec}_GACT_raw_reads.txt")
    conda:
        "envs/py.yml"
    params:
        window_size=50,
        threshold=0.95
    script:
        "scripts/GACT_reads.py"

rule GA_CT_fail_reads:
    input: 
        fastq_gzip_file=OUT_DIR + "{spec}/mapped_reads/{spec}_fail.fq.gz"
    output:
        out=OUT_DIR + "{spec}/list_multimapped_fail_reads_ends.txt"
    conda:
        "envs/py.yml"
    params:
        window_size=50,
        threshold=0.95
    script:
        "scripts/GACT_reads.py"

############## map raw and failed reads

rule minimap_raw_sort: #wrapper for minimaps2_bam_sorted
    input:
        target=get_assembly,
        query=get_fastq # or get_raw_reads in BAM
    output:
        OUT_DIR + "{spec}/mapped_reads/{spec}_raw_sorted.bam"
    threads: 6
    resources:
        mem_mb=100000
    params:
        extra="-x map-hifi",  # optional
        sorting="coordinate"  # optional: Enable sorting. Possible values: 'none', 'queryname' or 'coordinate'
    wrapper:
        "v3.12.0/bio/minimap2/aligner"

rule samtools_index_raw:
    input:
        OUT_DIR + "{spec}/mapped_reads/{spec}_raw_sorted.bam"
    output:
        OUT_DIR + "{spec}/mapped_reads/{spec}_raw_sorted.bam.bai"
    threads: 4
    wrapper:
        "v3.7.0/bio/samtools/index"

##############

# Wrapper for Minimap bam sorted:
 
rule minimap_fail_sort:
    input:
        target=get_assembly,
        query=rules.zip_fastq_fail.output
    output:
        OUT_DIR + "{spec}/mapped_reads/{spec}_fail_sorted.bam"
    threads: 6
    resources:
        mem_mb=100000
    params:
        extra="-x map-hifi",  # optional
        sorting="coordinate",  # optional: Enable sorting. Possible values: 'none', 'queryname' or 'coordinate'
        sort_extra="",  # optional: extra arguments for samtools/picard
    wrapper:
        "v3.12.0/bio/minimap2/aligner"

rule samtools_index_fail:
    input:
        OUT_DIR + "{spec}/mapped_reads/{spec}_fail_sorted.bam"
    output:
        OUT_DIR + "{spec}/mapped_reads/{spec}_fail_sorted.bam.bai"
    threads: 3
    wrapper:
        "v3.7.0/bio/samtools/index"

## plot coverage curve (main reads + ends into /extra/), write also the plotting table into /extra/

rule get_ends_prep_plot:
    input: 
        ass_regions=rules.GA_CT_assembly.output,
        reads_regions=rules.GA_CT_reads.output,
        aligned_reads=rules.minimap_raw_sort.output,
        aligned_reads_index=rules.samtools_index_raw.output,
    output:
        table_ends=OUT_DIR + "{spec}/list_contigs_ends.txt",
        plot_ends=OUT_DIR + "{spec}/PLOTS/curves_contigs_ends.pdf",
        table_plot_all=OUT_DIR + "{spec}/PLOTS/table_for_plot_curves_all.txt",
        samtools_depths=OUT_DIR + "{spec}/ass_regions_flank.depth"
    params:
        depth_prefix=r"ass_regions_flank",
    	plotting_flank=10000,
        path=OUT_DIR,
        ref=get_assembly,
        species=r"{spec}"
    conda: 
    	"envs/r.yml"
    resources:
        mem_mb=100000
    script:
        "scripts/GACT_get_ends.R"

rule plot_curves_all:
    input:
        table=rules.get_ends_prep_plot.output.table_plot_all,
    output:
        plot=OUT_DIR + "{spec}/PLOTS/curves_assembly_all.pdf"
    params:
        path=OUT_DIR
    conda:
        "envs/r.yml"
    resources:
        mem_mb=200000
    script:
        "scripts/GACT_plot_curves_all.R"

rule plot_fail_curves:
    input:
        ass_regions=rules.GA_CT_assembly.output,
        reads_regions=rules.GA_CT_fail_reads.output,
        aligned_reads=rules.minimap_fail_sort.output,
        aligned_reads_index=rules.samtools_index_fail.output,
        ref=get_assembly
    output:
        plot=OUT_DIR + "{spec}/curves/{spec}_GACT_fail_reads.pdf",
        plot_ends=OUT_DIR + "{spec}/curves/{spec}_GACT_fail_reads_ends.pdf",
        table_plot=OUT_DIR + "{spec}/curves/{spec}_GACT_fail_reads_for_plot.txt"
    params:
        depth_prefix=r"{spec}_ass_regions_flank",
        species=r"{spec}",
        plotting_flank=10000,
        path=OUT_DIR
    resources:
        mem_mb=200000
    conda:
        "envs/r.yml"
    script:
        "scripts/GACT_plot_coverages.R"

# analyse multimapped reads
rule get_multimapped:
    input:
        aligned_reads=rules.minimap_raw_sort.output
    output:
        OUT_DIR + "{spec}/multimapped/{spec}_mm_reads.txt"
    resources:
        mem_mb=20000
    shell:
        """
        module load bioinfo/samtools/1.19; samtools view -F 0x104 {input} | awk '$6 ~ /H|S/{{print $0}}' | awk '{{print $1, $2, $3, $4, $6, length ($10)-1 }}'  > {output}
        """

rule multimapped:
    input:
        ass_regions=rules.GA_CT_assembly.output,
        reads_regions=rules.GA_CT_reads.output,
        multimapped_reads=rules.get_multimapped.output,
        assembly=get_assembly
    output:
        targets=OUT_DIR + "{spec}/list_multimapped_raw_reads_ends.txt", #table with multimapped reads potential for scaffolding
        locs=OUT_DIR + "{spec}/multimapped/{spec}_locations_for_IGV.txt" #for IGV shots
    params:
        species=r"{spec}",
        path=OUT_DIR
    resources:
        mem_mb=200000
    conda:
        "envs/r.yml"
    script:
        "scripts/GACT_multimapped.R"


